apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion targetSdk
    defaultConfig {
        applicationId "ru.inwords.inwords"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode 16
        versionName "1.10.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_URL", "\"https://api.inwords.ru/\""
        buildConfigField "String", "GRPC_API_URL", "\"grpc.inwords.ru\""
        buildConfigField "String", "TTS_GOOGLE_API_URL", "\"https://texttospeech.googleapis.com\""
        buildConfigField "String", "POLICY_PRIVACY_URL", "\"https://inwords.ru/policy/privacy.html\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [crashlytics: "true"]
        }
        debug {
            manifestPlaceholders = [crashlytics: "false"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    viewBinding {
        enabled = true
    }

    androidExtensions {
        features = ["parcelize"]
        //TODO its now no need to use this to Parcelize, but it shows warning otherwise
    }

    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir '..\\..\\WebApi\\InWords.WebApi\\InWords.Protobuf\\'
            }
        }
    }

    testOptions {
        unitTests.all {
            // Always run tests, even when nothing changed.
            dependsOn 'clean'

            // Show test results.
            testLogging {
                events "passed", "skipped", "failed"
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/INDEX.LIST'
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$versions.PROTOC"
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:$versions.PROTOC"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$versions.GRPC"
        }
    }
    clean {
        delete generatedFilesBaseDir
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(path: ':flipview')
    implementation project(path: ':shimmer_recycler')
    implementation project(path: ':designcorelib')

    implementation 'com.google.firebase:firebase-analytics:17.2.3'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'

    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    //support
    implementation "androidx.appcompat:appcompat:$versions.ANDROID_X"

    implementation "androidx.recyclerview:recyclerview:$versions.RECYCLER"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0-rc01"

    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"

    implementation "androidx.constraintlayout:constraintlayout:$versions.CONSTRAINT_LAYOUT"

    //preference
    implementation 'androidx.preference:preference-ktx:1.1.0'

    //android core ktx
    implementation "androidx.core:core-ktx:$versions.CORE_KTX"

    // Fragment
    implementation "androidx.fragment:fragment:$versions.FRAGMENT"


    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.NAVIGATION"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.NAVIGATION"

    // Work manager
    implementation "androidx.work:work-runtime-ktx:$versions.WORK_MANAGER"
    implementation "androidx.work:work-rxjava2:$versions.WORK_MANAGER"

    // RxJava
    implementation "io.reactivex.rxjava3:rxjava:$versions.RX_JAVA"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.RX_ANDROID"

    //Fresco
    implementation('com.facebook.fresco:fresco:2.1.0') {
        exclude group: 'com.facebook.soloader', module: 'soloader'
    }
    implementation("com.facebook.fresco:imagepipeline-okhttp3:2.1.0") {
        exclude group: 'com.facebook.soloader', module: 'soloader'
    }

    //Shimmer
    implementation "com.facebook.shimmer:shimmer:$versions.SHIMMER"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.RETROFIT"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.RETROFIT"
    implementation "com.squareup.retrofit2:converter-gson:$versions.RETROFIT"
    //implementation "com.squareup.retrofit2:retrofit-mock:$versions.RETROFIT"

    implementation "com.squareup.okhttp3:okhttp:$versions.OK_HTTP"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.OK_HTTP"

    // Room components
    implementation "androidx.room:room-runtime:$versions.ROOM"
    implementation "androidx.room:room-rxjava2:$versions.ROOM"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "androidx.room:room-compiler:$versions.ROOM"

    //androidTestImplementation "android.arch.persistence.room:testing:$versions.ROOM"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.LIFECYCLE"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.LIFECYCLE"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.DAGGER"
    kapt "com.google.dagger:dagger-compiler:$versions.DAGGER"
    implementation "com.google.dagger:dagger-android:$versions.DAGGER"
    // if you use the support libraries
    implementation "com.google.dagger:dagger-android-support:$versions.DAGGER"
    kapt "com.google.dagger:dagger-android-processor:$versions.DAGGER"

    // You need to build grpc-java to obtain these libraries below.
    implementation "io.grpc:grpc-cronet:$versions.GRPC"
    implementation 'com.google.android.gms:play-services-cronet:17.0.0'
    implementation 'org.chromium.net:cronet-fallback:76.3809.111'

    implementation "io.grpc:grpc-protobuf-lite:$versions.GRPC"
    implementation "io.grpc:grpc-stub:$versions.GRPC"
    implementation "io.grpc:grpc-android:$versions.GRPC"
    implementation "io.grpc:grpc-core:$versions.GRPC"
    implementation "io.grpc:grpc-api:$versions.GRPC"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"

    testImplementation "io.mockk:mockk:1.9.3"
    //androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
