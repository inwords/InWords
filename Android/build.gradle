// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        // SDK and Build tools version
        minSdk = 21
        targetSdk = 29
        buildTools = "29.0.6"

        kotlin_version = '1.3.72'

        jacoco_version = '0.8.5'

        versions = [
                ANDROID_X        : '1.1.0',
                MATERIAL_COMP    : '1.0.0',
                CONSTRAINT_LAYOUT: '1.1.3',
                NAVIGATION       : '2.2.2',
                WORK_MANAGER     : '2.3.4',
                CORE_KTX         : '1.2.0',
                RX_JAVA          : '2.2.9',
                RX_ANDROID       : '2.1.1',
                RX_BINDING       : '2.2.0',
                RETROFIT         : '2.8.1',
                OK_HTTP          : '4.6.0',
                ROOM             : '2.2.5',
                LIFECYCLE        : '2.2.0',
                PREFERENCE       : '1.1.1',
                DAGGER           : '2.27',
                SHIMMER          : '0.4.0',
                RECYCLER         : '1.1.0',
                FRAGMENT         : '1.3.0-alpha05',
                PROTOC           : '3.11.4',
                GRPC             : '1.29.0',
                GSON             : '2.8.6',
                GROUPIE          : '2.8.0',
                ANDROIDX_TEST    : '1.2.0',
                ANDROIDX_TEST_EXT: '1.1.1',
                JUNIT_JUPITER    : '5.6.2',
                MOCKK            : '1.9.3'
        ]
    }

    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.2.0"
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'io.fabric.tools:gradle:1.31.2' //crashlytics
        classpath "org.jacoco:org.jacoco.core:0.8.5"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.NAVIGATION"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.12"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { subproject ->
    subproject.plugins.whenPluginAdded { plugin ->
        if (isAndroidLibraryOrAppPlugin(plugin)) {
            subproject.apply from: "${project.rootDir.absolutePath}/android-subproject.gradle"
        }
    }
}

static boolean isAndroidLibraryOrAppPlugin(Plugin plugin) {
    plugin.getClass().canonicalName in ["com.android.build.gradle.LibraryPlugin",
                                        "com.android.build.gradle.AppPlugin"]
}

final def (JacocoMerge mergeTask, JacocoReport mergedReportTask) = addJacocoMergeToRootProject(rootProject)
ext.mergeTask = mergeTask
ext.mergedReportTask = mergedReportTask

protected static addJacocoMergeToRootProject(final Project project) {
    project.plugins.apply('jacoco')

    project.jacoco {
        toolVersion = project.ext.jacoco_version
    }

    def mergeTask = project.task("mergeJacocoReports", type: JacocoMerge) {
        executionData project.files().asFileTree // Start with an empty collection.
        destinationFile project.file("${project.buildDir}/jacoco/mergedReport.exec")

        doFirst {
            // Filter non existing files.
            def realExecutionData = project.files()

            executionData.each {
                if (it.exists()) {
                    realExecutionData.setFrom(project.files(it) + realExecutionData.files)
                }
            }

            executionData = realExecutionData
        }
    }

    def mergedReportTask = project.task("jacocoTestReportMerged", type: JacocoReport, dependsOn: mergeTask) {
        group = "Reporting"

        executionData mergeTask.destinationFile

        reports {
            html.enabled = true
            xml.enabled = true
        }

        // Start with empty collections.
        getClassDirectories().from(project.files())
        getAdditionalSourceDirs().from(project.files())
        getSourceDirectories().from(project.files())
    }

    return [mergeTask, mergedReportTask]
}