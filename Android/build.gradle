// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        // SDK and Build tools version
        minSdk = 23
        targetSdk = 31
        buildTools = "29.0.6"

        kotlin_version = '1.6.20'

        jacoco_version = '0.8.7'

        versions = [
                ANDROID_X        : '1.4.1',
                MATERIAL_COMP    : '1.5.0',
                CONSTRAINT_LAYOUT: '2.1.3',
                NAVIGATION       : '2.4.2',
                WORK_MANAGER     : '2.7.1',
                CORE_KTX         : '1.7.0',
                RX_JAVA          : '2.2.21',
                RX_ANDROID       : '2.1.1',
                OK_HTTP          : '4.9.3',
                ROOM             : '2.4.2',
                LIFECYCLE        : '2.4.1',
                PREFERENCE       : '1.2.0',
                DAGGER           : '2.41',
                SHIMMER          : '0.5.0',
                RECYCLER         : '1.2.1',
                FRAGMENT         : '1.4.1',
                PROTOC           : '3.19.4',
                GRPC             : '1.44.1',
                GSON             : '2.9.0',
                GROUPIE          : '2.9.0',
                ANDROIDX_TEST    : '1.4.0',
                ANDROIDX_TEST_EXT: '1.1.3',
                JUNIT_JUPITER    : '5.8.2',
                MOCKK            : '1.12.2'
        ]
    }

    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$rootProject.ext.kotlin_version"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.8.2.0"
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'
        classpath "org.jacoco:org.jacoco.core:0.8.7"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.NAVIGATION"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.18"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { subproject ->
    subproject.plugins.whenPluginAdded { plugin ->
        if (isAndroidLibraryOrAppPlugin(plugin)) {
            subproject.apply from: "${project.rootDir.absolutePath}/android-subproject.gradle"
        }
    }
}

static boolean isAndroidLibraryOrAppPlugin(Plugin plugin) {
    plugin.getClass().canonicalName in ["com.android.build.gradle.LibraryPlugin",
                                        "com.android.build.gradle.AppPlugin"]
}

final def (JacocoMerge mergeTask, JacocoReport mergedReportTask) = addJacocoMergeToRootProject(rootProject)
ext.mergeTask = mergeTask
ext.mergedReportTask = mergedReportTask

protected static addJacocoMergeToRootProject(final Project project) {
    project.plugins.apply('jacoco')

    project.jacoco {
        toolVersion = project.ext.jacoco_version
    }

    def mergeTask = project.task("mergeJacocoReports", type: JacocoMerge) {
        executionData project.files().asFileTree // Start with an empty collection.
        destinationFile project.file("${project.buildDir}/jacoco/mergedReport.exec")

        doFirst {
            // Filter non existing files.
            def realExecutionData = project.files()

            executionData.each {
                if (it.exists()) {
                    realExecutionData.setFrom(project.files(it) + realExecutionData.files)
                }
            }

            executionData = realExecutionData
        }
    }

    def mergedReportTask = project.task("jacocoTestReportMerged", type: JacocoReport, dependsOn: mergeTask) {
        group = "Reporting"

        executionData mergeTask.destinationFile

        reports {
            html.enabled = true
            xml.enabled = true
        }

        // Start with empty collections.
        getClassDirectories().from(project.files())
        getAdditionalSourceDirs().from(project.files())
        getSourceDirectories().from(project.files())
    }

    return [mergeTask, mergedReportTask]
}